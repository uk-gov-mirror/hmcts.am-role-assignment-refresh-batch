buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.4.34")
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.15.0'
    }
}

plugins {
    id 'application'
    id 'uk.gov.hmcts.java' version '0.12.67'
    id 'pmd'
    id 'jacoco'
    id 'org.springframework.boot' version '3.3.7'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'org.sonarqube' version '7.0.1.6134'
    id 'org.owasp.dependencycheck' version '12.1.6'
}

apply plugin: "info.solidsoft.pitest"
apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

ext['snakeyaml.version'] = '2.0'

def versions = [
        junit          : '5.9.3',
        lombok         : '1.18.42',
        reformLogging  : '6.1.9',
        reformS2sClient: '5.3.3',
        serenity       : '4.2.34',
        sonarPitest    : '0.5',
        pitest         : '1.21.0',
        spring         : '6.2.12',
        springSecurity : '6.5.5',
        rest_assured   : '5.5.6',
        feign_jackson  : '13.6',
        jacksondata    : '2.18.3',
        log4JVersion   : '2.24.3',
        logbackVersion : '1.3.14',
        thymeleaf      : '3.1.3.RELEASE'
]

ext {
    springCloudVersion = '2023.0.6'
}

mainClassName = 'uk.gov.hmcts.reform.roleassignmentrefresh.RoleAssignmentRefreshApplication'
def jarName = 'am-role-assignment-refresh-batch.jar'

pmd {
    toolVersion = '7.17.0'
    sourceSets = []
}

group = 'uk.gov.hmcts.reform.roleassignmentrefresh'
version = '0.0.1'

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
        }
    }
}


sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            compileClasspath += main.compileClasspath
            compileClasspath += test.compileClasspath
            runtimeClasspath += main.output
            runtimeClasspath += main.runtimeClasspath
            runtimeClasspath += test.runtimeClasspath

            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

idea {
    module {
        // config to allow IntelliJ to mark test source and resource files correctly to help linting tools
        testSources.from(java.sourceSets.integrationTest.java.srcDirs)
        testResources.from(java.sourceSets.integrationTest.resources.srcDirs)
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
    finalizedBy aggregate
}
check.dependsOn integration

task fortifyScan(type: JavaExec)  {
    main("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
    // The line below prevents the build from failing if the Fortify scan detects issues
    ignoreExitValue = true as JavaExecSpec
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport

jacoco {
    toolVersion = "0.8.14"
}

pitest {
    pitestVersion = versions.pitest
    junit5PluginVersion = '1.2.1'
    targetClasses = ['uk.gov.hmcts.reform.*']
    excludedClasses = ['uk.gov.hmcts.reform.roleassignmentrefresh.*Application',
                       '*config*',
                       '*exception*',
                       '*oidc*',
                       '*RefreshORMRules*,',
                       'uk.gov.hmcts.reform.roleassignmentrefresh.util.Swagger2SpringBoot']
    excludedMethods = ['']
    threads = 15
    outputFormats = ['XML', 'HTML']
    timestampedReports = true
    mutationThreshold = 50
}

sonarqube {
    properties {
        property "sonar.projectName", "am-role-assignment-refresh-batch"
        property "sonar.projectKey", "am-role-assignment-refresh-batch"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "**config**," +
                "src/main/java/uk/gov/hmcts/reform/roleassignmentrefresh/data/Refresh*," +
                "**Application.java,"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all {
            def isNonStable = { String version ->
                ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
                }
            }
            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

//https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
    }
}


dependencies {
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-batch')
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa')
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation(group: 'org.springframework', name: 'spring-core', version: versions.spring)
    implementation(group: 'org.springframework', name: 'spring-beans', version: versions.spring)
    implementation(group: 'org.springframework', name: 'spring-context', version: versions.spring)
    implementation(group: 'org.springframework', name: 'spring-context-support', version: versions.spring)
    implementation(group: 'org.springframework', name: 'spring-core', version: versions.spring)

    implementation group: 'org.springframework.security', name: 'spring-security-web', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-core', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-config', version: versions.springSecurity
    implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: versions.springSecurity

    implementation group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.reformS2sClient
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.8'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation 'commons-io:commons-io:20030203.000550'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksondata
    implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: versions.jacksondata, ext: 'pom'
    implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.4'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '5.2.1'
    implementation 'org.jetbrains:annotations:26.0.2-1'
    implementation group: 'org.springframework', name: 'spring-aop', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-aspects', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-expression', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-jcl', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-jdbc', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-orm', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-tx', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-web', version: versions.spring
    implementation group: 'org.springframework', name: 'spring-webmvc', version: versions.spring
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.6.0'
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'

    implementation group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-instrumentation-annotations', version: '2.21.0'
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '3.7.5'

    //Sendgrid
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'
    implementation group: 'org.thymeleaf', name: 'thymeleaf', version: versions.thymeleaf
    implementation group: 'org.thymeleaf', name: 'thymeleaf-spring5', version: versions.thymeleaf
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation 'com.sendgrid:sendgrid-java:4.10.3'

    implementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.17.8'
    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'
    testImplementation 'org.springframework.batch:spring-batch-test:5.2.4'
    testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'junit', module: 'junit'
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-websocket"
        exclude group: "org.apache.tomcat.embed", module: "tomcat-embed-core"
    }

    pitest 'org.pitest:pitest-junit5-plugin:1.2.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

    testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
    testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
    testImplementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.15.0'
    testImplementation group: 'org.pitest', name: 'pitest-junit5-plugin', version: '1.2.3'

    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.rest_assured
    testImplementation group: 'io.github.openfeign', name: 'feign-jackson', version: versions.feign_jackson
    testImplementation('com.opentable.components:otj-pg-embedded:1.1.1')
    testImplementation 'com.github.hmcts:fortify-client:1.4.10:all'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

}

configurations.all {
    exclude group: 'org.springframework.security', module: 'spring-security-rsa'
    exclude group: 'org.codehaus.jettison', module: 'jettison'
}

test {
    useJUnitPlatform()
    failFast = true
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootJar {
    archiveFileName = jarName

    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processIntegrationTestResources") {
    duplicatesStrategy = 'include'
}

configurations {
    runtime.exclude group: "org.glassfish", module: "jakarta.el"
}
